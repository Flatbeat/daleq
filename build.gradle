configure(allprojects) {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'

    group = 'de.brands4friends.daleq'
    version = '0.1.0'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile "junit:junit:4.8.2"
        testCompile "org.hamcrest:hamcrest-library:1.3.RC2"
        testCompile "org.easymock:easymock:3.1"
        testCompile "org.slf4j:slf4j-simple:1.5.6"
    }

//    // workaround for gradle bug. no debug var symbols in grade 1-09m
//    tasks.withType(Compile) { options.compilerArgs = ["-g:source,lines,vars"] }

    // Define Dependencies
//    apply from: "${rootProject.projectDir}/gradle/dependencies.gradle"

    test {
        maxHeapSize '800M'
//        debug true
        maxParallelForks 10
    }

    checkstyle {
        configFile file("${rootProject.projectDir}/config/checkstyle/checkstyle.xml")
    }

    pmd {
        ruleSets = ['basic','design','braces','codesize','clone','strings','optimizations']
        ruleSetFiles = files("${rootProject.projectDir}/config/pmd/ruleset.xml")
    }
}

project('daleq') {
    dependencies {
        compile "joda-time:joda-time:1.6.2"
        compile "com.google.guava:guava:12.0"
        compile "commons-lang:commons-lang:2.6"
        compile "org.dbunit:dbunit:2.4.8"
        compile "dom4j:dom4j:1.6.1"

        testCompile "nl.jqno.equalsverifier:equalsverifier:1.1.3"
    }
}

project('examples') {
    dependencies {
        compile project(':daleq')
        compile "org.springframework:spring-jdbc:3.1.1.RELEASE"
        testCompile "org.springframework:spring-test:3.1.1.RELEASE"
        testCompile "org.hsqldb:hsqldb:2.2.8"
    }
}
