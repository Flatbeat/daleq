/*
 * Copyright 2012 brands4friends, Private Sale GmbH
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */









apply plugin: 'maven'
apply plugin: 'signing'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

artifacts {
    archives jar

    archives sourcesJar
    archives javadocJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories.mavenDeployer {
        configuration = configurations.archives

        repository(url: propertyOr('nexus.url', null)) {
            authentication(
                    userName: propertyOr('nexus.user', null),
                    password: propertyOr('nexus.password', null)
            )
        }

        pom.project {
            name project.displayName
            description project.description
            packaging 'jar'
            url 'https://github.com/brands4friends/daleq'

            scm {
                url 'https://github.com/brands4friends/daleq'
                connection 'scm:git:https://github.com/brands4friends/daleq.git'
            }

            licenses {
                license {
                    name 'The Apache License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'lgirndt'
                    name 'Lars Girndt'
                    email 'lars.girndt@brands4friends.com'
                }
            }
        }
    }
}
